# Top-Down Parsing

## 1. Recursive Descent Parser

* **Overview**

```
exp → exp addop term | term
addop → + | -
term → term mulop factor | factor
mulop → *
factor → ( exp ) | number
```

위와 같은 문법이 있을 때 `factor` 를 알아보는 로직은 다음과 같다.

```
procedure factor;
begin
	case token of
	(:	match(();
			exp;
			match());
	number:
			match(number);
	else error;
	end case;
end factor;
```

여기서 `match()` 는 주어지는 인풋 값과 매칭되어야 하는 토큰이 맞는지를 검사하는 함수이며 `exp` 는 또 다른 procedure라고 보면 된다. 이런식으로 procedure 안에서 토큰의 매칭 여부와 다른 procedure를 호출하여 parsing 하는 방식을 recursive descent parsing이라고 한다.

* **BNF → EBNF**

하지만 아래 문법과 같이 다음 parsing을 사용하기 어려운 경우가 있다.

```
if-stmt → if ( exp ) stmt | if ( exp ) stmt else stmt
```

위 경우는 즉각적으로 분기를 생성하기가 어렵기 때문에 BNF를 EBNF로 변환하여 파싱한다.

```
if-stmt → if ( exp ) stmt [else stmt]
```

또한 아래의 경우와 같이 left-recursion이 있는 문법의 경우에도 문제가 된다.

```
exp → exp addop term | term
```

`exp` 의 procedure를 호출하게 되면 재귀호출이 되기 때문에 infinite loop에 빠지게 된다. 따라서 이것 또한 EBNF로 변환하여야 한다.

```
exp → term { addop term }
```

* **Limitation**
  * 모든 문법을 EBNF로 변환할 수 있는 것은 아니다. 안될때가 있다.
  * `A → a | b` 와 같은 경우에 a,b, 모두 N.T라면  `A → a` 를 쓸지 `A → b` 를 쓸지 결정하기가 어렵다.



## 2. LL(1) Parser

* **Overview**

LL(1) Parser는 아래와 같은 순서로 이루어진다.

1. Left-recursion removal과 Left-factoring을 통해 문법을 맞게 변환한다.
2. 각각의 N.T에 대해 First Set과 Follow Set을 구한다.
3. First Set과 Follow Set을 사용하여 Parsing table을 만든다.
4. Parsing table을 사용하여 Parsing stack으로 파싱한다.

Parsing stack의 가장 위에 있는 것이 토큰이면 input token과 매칭하는지 보고 N.T 라면 parsing table과 input token(L.A symbol)을 보고 그에 맞는 derivation을 역방향으로 집어넣는다.

이런식으로 `$` 가 parsing stack과 input token에 모두 있을 때까지 계속하며 마지막에 만나게 되면 파싱이 성공한 것이다.

* **Left-Recursion Removal**

문법의 left-associativity를 위해서 left-recursion이 발생하지만 이걸 LL(1) parsing에 그대로 사용하게 되면 parsing stack에 infinitely 들어가게 되기 때문에 제거를 해줘야 한다. Left-recursion에는 *immediate left-recursion*과 *indirect left-recursion*이 존재하는데, 제거하는 방법은 동일하다.

1. <u>Immediate left-recursion removal</u>

```
exp → exp addop term | term
--------------------------- (1)
exp → term exp'
exp' → addop term exp' | epsilon
```

2. <u>Indirect left-recursion removal</u>

```
A → Ba | Aa | c
B → Bb | Ab | d
--------------- (1)
A → BaA' | cA'
A' → aA' | epsilon
B → Bb | BaA'b | cA'b | d
------------------------- (2)
A → BaA' | cA'
A' → aA' | epsilon
B → cA'bB' | dB'
B' → bB' | aA'bB' | epsilon
```

* **Left-Factoring**

Production rule에서 여러개의 choice가 있는 경우에, 똑같은 prefix를 가진다면 어떤 production을 수행해야 할지 알 수 없다. 따라서 공통된 prefix를 <u>"factor"</u> 하여 문법을 변환하여야 한다.

```
stmt → assign-stmt | call-stmt | other
assign-stmt → identifier := exp
call-stmt → identifier ( exp-list )
-------------------------------------- (left-factoring 하기 위해 변환)
stmt → identifier := exp | identifier ( exp-list ) | other
---------------------------------------------------------- (left-factoring)
stmt → identifier stmt' | other
stmt' → := exp | ( exp-list )
```

* **First Set**

Recursive descent parser에서 가장 처음으로 들었던 예시에 대해 left-recursion removal을 적용한 다음 first set을 구해보도록 하자. 먼저 left-recursion이 적용된 경우이다.

```
exp → term exp'
exp' → addop term exp' | epsilon
addop → + | -
term → factor term'
term' → mulop factor term' | epsilon
mulop → *
factor → ( exp ) | number
```

1. First(exp) = First(term) = First(factor) = `{ (, number }`
2. First(exp') = { First(addop), $\epsilon$ } = `{ +, -, epsilon​ }`
3. First(addop) = `{ +, - }`
4. First(term) = `{ (, number }`
5. First(term') = { First(mulop), $\epsilon$ } = `{ *, epsilon​ }`
6. First(mulop) = `{ * }`
7. First(factor) = `{ (, number }`

* **Follow Set**

Follow Set도 구해보자. 주의할 점은 $\epsilon$ 이 포함되서는 안된다는 것!!

1. Follow(exp) = `{ $, ) }`
2. Follow(exp') = Follow(exp) = `{ $, ) }`
3. Follow(addop) = First(term) = `{ (, number }`
4. Follow(term) = { First(exp'), Follow(exp') } = `{ +, -, $, ) }`
5. Follow(term') = Follow(term) = `{ +, -, $, ) }`
6. Follow(mulop) = First(factor) = `{ (, number }`
7. Follow(factor) = { First(term'), Follow(term') } = `{ *, +, -, $, ) }`

* **Parsing Table Construction**

이제 구한 first set과 follow set을 통해 parsing table을 만들어야 한다. 그 규칙은 아래와 같다.

1) `A → a` 에서 First(a) 에 포함된 모든 토큰들에 대해서 `M[A,토큰]` 에 `A → a` 를 추가한다.  
2) 만약 First(a)가 $\epsilon$ 을 포함한다면 Follow(A)에 포함된 모든 토큰들에 대해서 `M[A, 토큰]` 에 `A → a` 를 추가한다.

위 규칙을 적용했을 때 아래와 같은 parsing table을 만들 수 있다.

<img src="https://user-images.githubusercontent.com/35518072/59011711-7f99a080-8870-11e9-833c-048c658ab10f.png" width="500px">

* **Parsing stack과 parsing table을 사용해서 LL(1) parsing 하기**

Parsing table을 통해서 derivation을 역으로 스택에 삽입하면 된다. Input string이 `3+4+5` 라고 하자.

```
$ exp						           3+4+5 $  (exp → term exp')
$ exp' term                3+4+5 $  (term → factor term')
$ exp' term' factor        3+4+5 $  (factor → number)
$ exp' term' number        3+4+5 $  (match!!) 
$ exp' term'                +4+5 $  (term' → epsilon)
$ exp'                      +4+5 $  (exp' → addop term exp')
$ exp' term addop           +4+5 $  (addop → +)
$ exp' term +               +4+5 $  (match!!)
$ exp' term                  4+5 $  (term → factor term')
$ exp' term' factor          4+5 $  (factor → number)
$ exp' term' number          4+5 $  (match!!)
$ exp' term'                  +5 $  (term' → epsilon)
$ exp'                        +5 $  (exp' → addop term exp')
$ exp' term addop             +5 $  (addop → +)
$ exp' term +                 +5 $  (match!!)
$ exp' term                    5 $  (term → factor term')
$ exp' term' factor            5 $  (factor → number)
$ exp' term' number            5 $  (match!!)
$ exp' term'                     $  (term' → epsilon)
$ exp'                           $  (exp' → epsilon)
$                                $  (ACCEPT!!!!)
```



# Bottom-Up Parsing

LL(1) Parsing을 사용하게 되면, 주어진 CFG에 left-recursion removal과 left-factoring을 해줘야 한다는 단점이 있다. 따라서 위 작업을 하지 않고도 parsing 할 수는 없을까? 하고 만들어진 개념이 바로 bottom-up parsing이다.

## 1. LR(0) Parser

* **Overview**

Bottom-up parsing 중에서도 가장 기초적인 parser가 LR(0) parser 인데, 기본적으로 LR(0) items 라는 것을 사용하며 이를 이용해 DFA를 구성한다. 그 다음 DFA를 이용해 parsing table construction을 하고 이를 이용해 파싱하는 방식이다. Bottom-up parsing의 경우, starting N.T 부터 parsing stack에 넣는 top-down parsing 과는 다르게 아무것도 없이 시작한다. 완전히 다른 방법을 사용하는데, input string을 보고 parsing stack에 집어넣거나 현재 arbitrary depth of stack을 보고 reduction(production의 반대개념)을 하는 방식이다.

* **LR(0) Items**

LR(0) items는 간단하게 말해서 각 production의 progress가 어느정도인지, 즉 arbitrary depth of stack을 알아야 하기 때문에 각 progress에 따라서 DFA를 구성하기 위해 사용한다. 아래 문법을 사용해서 LR(0) items를 구해보자.

```
A' → A
A → (A) | a
```

위 CFG가 형성하는 모든 derivation progress를 본다면 아래와 같다. 여기서 점(period)은 어디까지 봤는지를 나타낸다.

```
A' → .A
A' → A.
A → .(A)
A → (.A)
A → (A.)
A → (A).
A → .a
A → a.
```

위에 나열된 것이 LR(0) items 이며 여기서 점이 가장 처음에 있는 것을 initial item, 마지막에 있는 것을 complete item 이라고 한다. Initial item은 말 그대로 아직 어떠한 것도 알아보지 않은 것이며 complete item은 모든 것을 알아본 것이다.

* **DFA of LR(0) Items**

원래는 NFA를 구성한 뒤에 $\epsilon$ - closure 를 활용하여 DFA를 구성하는 것이 정석이지만 위와 같은 보다 간단한 LR(0) items의 경우 바로 DFA를 구성할 수 있기 때문에 바로 구하도록 하겠다.

<img src="https://user-images.githubusercontent.com/35518072/59014708-38171280-8878-11e9-91c6-8f34755c175b.png" width="500px">

* **Parsing Table Construction**

DFA를 이용해서 parsing table을 만들 수 있는데, 각 state에 대해 input token이나 N.T가 뭐냐에 따라 transition이 이루어지는 방식으로 만들어진다.

<img src="https://user-images.githubusercontent.com/35518072/59014474-ad361800-8877-11e9-8f10-21eb226f3714.png" width="600px">

<u>Shift</u>란 input token을 parsing stack으로 올린다는 뜻이고, <u>reduce</u>란 arbitrary depth of stack을 알고 있을 때 원래의 production rule에 따라 N.T를 이끌어내는 것이다.

* **LR(0) Parsing**

<img src="https://user-images.githubusercontent.com/35518072/59014860-93e19b80-8878-11e9-8513-443485e7c837.png" width="500px">

이제 parsing table을 사용하여 parsing을 해보자. 처음에 parsing stack에는 start state가 있고 input token을 참고하여 parsing table을 보면서 shift를 할지, reduce를 할지 정하게 된다. 그 과정을 따라가보면 다음과 같다.

1. 0에서 input token이 (일 경우에 3으로 shift가 일어나서 `(3` 이 스택에 삽입된다.
2. 3에서 input token이 (일 경우에 3으로 shift가 일어나서 `(3` 이 스택에 삽입된다.
3. 3에서 input token이 a일 경우에 2로 shift가 일어나서 `a2` 가 스택에 삽입된다.
4. 2에서 `A → a` 에 대한 reduce가 일어나고 3에서 N.T 인 A가 들어옴에 따라 `A4` 가 스택에 삽입된다.
5. 4에서 input token이 )인 경우에 5로 shift가 일어나서 `)5` 가 스택에 삽입된다.
6. 5에서 `A → (A)` 에 대한 reduce가 일어나고 3에서 N.T 인 A가 들어옴에 따라 `A4` 가 스택에 삽입된다.
7. 5에서 `A → (A)` 에 대한 reduce가 일어나고 0에서 N.T 인 A가 들어옴에 따라 `A1` 이 스택에 삽입된다.
8. 상태 1로 왔으므로 accept가 이루어진다.



## 2. SLR(1) Parser

* **Overview**

LR(0) Parser의 경우 parsing을 할 때 L.A symbol을 아예 보지 않기 때문에 conflict가 많이 발생할 수 있다. 여기서 말하는 conflict란 2가지 종류가 있는데 shift-reduce conflict와 reduce-reduce conflict이다.(줄여서 S/R conflict, R/R conflict)

S/R conflict란 하나의 state에 shifting item과 complete item이 모두 있는 경우로 shift를 해야할 지 reduce를 해야할 지 알 수 없는 문제상황을 말한다. R/R conflict는 complete item이 2개 있는 상황에서 어떤 것으로 reduce를 해야할 지 알 수 없는 문제상황을 말한다. LR(0) parser가 이런 conflict가 제일 많은 편이며 그걸 줄여주기 위해 점점 parsing 과정이 강화되는데 그 첫번째가 SLR(1) Parser 이다.

* **LR(0) Parser에 비해 개선된 점**

Parsing algorithm이 다른데, 1개의 L.A symbol을 확인하면서 현재 state에 있는 shifting이나 reduction을 선택하게 되어 기존에 LR(0) parsing에서 발생했던 S/R conflict를 해결할 수 있다. 아래 예시를 통해 확인해보자.

<img src="https://user-images.githubusercontent.com/35518072/59017358-5122c200-887e-11e9-9921-f28e4ccf72f6.png" width="500px">

State 1을 보면 shifting item도 있고 complete item도 있다는 것을 확인할 수 있다. 여기서 LR(0) parsing에선 L.A symbol을 보지 않기 때문에 S/R conflict가 생기지만 SLR(1)에선 해결된다. SLR(1)은 reduce를 하기전 input token이 complete item에 해당하는 N.T의 Follow에 포함되는지를 검사한다. 위에선 input token이 +인 경우에 Follow(E') = { $ } 이고 Follow(E) = { \$, + }인데 complete item인 `E' → E.` 에 대해 Follow(E')이 +를 포함하지 않기 때문에 reduction이 일어나지 않고 shift가 발생한다.

* **Disambiguating Rule**

S/R conflict에 대해선 대부분의 파서가 reduce 보다는 shift를 선호하는데, 이는 ambiguous grammar에 대해서 disambiguating rule을 적용하는 것과 동일하게 나타난다. 예를 들어, 아래의 S/R conflict에선 reduce보다 shift 하는 것을 통해 dangling-else problem에서의 most closely nested rule을 만족시킬 수 있다.

<img src="https://user-images.githubusercontent.com/35518072/59017937-b88d4180-887f-11e9-950b-5fea8a319909.png" width="500px">

State 5를 보면 S/R conflict가 발생하고 있는데, input token이 else일 때 shift를 하게 됨으로서 dangling-else problem에서의 문제를 most closely nested rule로 해결하고 있는 것을 볼 수 있다.

* **Limitation**

SLR(1)은 LR(0)에 비해 강해졌지만 그럼에도 불구하고 여전히 conflict가 존재할 수 있다. 애초에 LR(0) items를 통해서 DFA를 구성할 때 L.A symbol을 보지 않기 때문에 이것에 의한 conflict가 발생할 수 있는 것이다.

```
S → id | V := E
V → id
E → V | n
```

위 CFG에서 LR(0) items를 뽑아내보면 start state는 다음과 같다.

```
S' → .S
S → .id
S → .V := E
V → .id
```

이 때 L.A symbol이 `id` 인 경우엔 다음 state로 transition이 일어난다.

```
S → id.
V → id.
```

R/R conflict가 발생하는 것을 알 수 있다. 이는 DFA를 구성할 때 `:=` 를 보지 않고 구성하였기 때문에 발생한 것이다. Input symbol이 `$` 인 경우에 Follow(S)와 Follow(V)가 모두 `$` 를 포함하므로 2가지 경우 모두로 reduction이 발생할 수 있다. 잘 생각해보면 애초에 변수(V)가 statement의 마지막에 올 수 있다는 것이 말이 안된다. 따라서 이것이 바로 SLR(1) parser의 한계이다.



## 3. LR(1) Parser

* **Overview**

LR(0) parser의 문제를 SLR(1) parser가 해결했으니 이제 SLR(1) parser를 좀 더 강하게 만들 시간이다. 이제는 DFA를 구성할 때 L.A symbol을 보면서 구성하는 LR(1) parser를 사용하자. DFA를 구성할 때는, LR(0) item과 1개의 L.A symbol을 보는데 이걸 합해서 LR(1) item 이라고 한다. 

* **LR(1) Items**

```
A → (A) | a
```

아주 간단한 CFG를 통해 LR(1) items를 나열해보자. 이걸 통해서 LR(1) parser가 얼마나 복잡한지 경험해볼 수 있다.

1. State 0

```
[A' → .A, $]
[A → .(A), $]
[A → .a, $]
```

2. State 1

```
[A' → A., $]
```

3. State 2

```
[A → (.A), $]
[A → .(A), )]
[A → .a, )]
```

4. State 3

```
[A → a., $]
```

5. State 4

```
[A → (A.), $]
```

6. State 5

```
[A → (.A), )]
[A → .(A), )]
[A → .a, )]
```

7. State 6

```
[A → a. )]
```

8. State 7

```
[A → (A)., $]
```

9. State 8

```
[A → (A.), )]
```

10. State 9

```
[A → (A)., )]
```

State 2와 5를 보면 LR(0) items는 같지만 L.A symbol이 `$` 와 `)` 로 다르다는 것을 알 수 있다.

* **DFA of LR(1) Items**

<img src="https://user-images.githubusercontent.com/35518072/59019926-e2e0fe00-8883-11e9-9a6d-82c803700a71.png" width="500px">

정말 간단한 CFG 임에도 불구하고 DFA는 상당히 복잡하다.

* **Parsing Table Construction**

DFA가 복잡하기 때문에 당연히 parsing table의 크기도 클 수밖에 없다.

<img src="https://user-images.githubusercontent.com/35518072/59020057-2b98b700-8884-11e9-99fb-6fa1252aefe4.png" width="500px">

* **SLR(1) parser의 문제를 해결하는 방법**

SLR(1)의 한계로 설명된 R/R conflict의 경우, LR(1) parser에선 L.A symbol이 달라지기 때문에 해결된다. 위에서 예시로 삼은 CFG에서 R/R conflict가 발생하는 state만 보면 다음과 같이 나온다.

```
[S → id, $]
[V → id, :=]
```

위와 같이 `$` 와 `:=` 에 따라 reduce가 다르게 되기 때문에 R/R conflict가 해결됨을 알 수 있다.

